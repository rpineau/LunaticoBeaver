; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Lunatico Astro NexDome Beaver Dome Contoller X2 PlugIn"
#define MyAppVersion "1.4"
#define MyAppPublisher "RTI-Zone"
#define MyAppURL "https://rti-zone.org"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{BA4F03A6-6F4D-4075-ADBF-AA11C413BC68}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={code:TSXInstallDir}\Resources\Common
DefaultGroupName={#MyAppName}

; Need to customise these
; First is where you want the installer to end up
OutputDir=installer
; Next is the name of the installer
OutputBaseFilename=LunaticoBeaver_X2_Installer
; Final one is the icon you would like on the installer. Comment out if not needed.
SetupIconFile=rti_zone_logo.ico
Compression=lzma
SolidCompression=yes
; We don't want users to be able to select the drectory since read by TSXInstallDir below
DisableDirPage=yes
; Uncomment this if you don't want an uninstaller.
;Uninstallable=no
CloseApplications=yes
DirExistsWarning=no

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Dirs]
Name: "{app}\Plugins\DomePlugIns";
Name: "{app}\Plugins64\DomePlugIns";

[Files]
Source: "domelist LunaticoBeaver.txt";                              DestDir: "{app}\Miscellaneous Files"; Flags: ignoreversion
Source: "domelist LunaticoBeaver.txt";                              DestDir: "{app}\Miscellaneous Files"; Flags: ignoreversion; DestName: "domelist64 LunaticoBeaver.txt"
; 32 bits
Source: "libLunaticoBeaver\Win32\Release\libLunaticoBeaver.dll";    DestDir: "{app}\Plugins\DomePlugIns"; Flags: ignoreversion
Source: "LunaticoBeaver.ui";                                        DestDir: "{app}\Plugins\DomePlugIns"; Flags: ignoreversion
Source: "NexDome.png";                                              DestDir: "{app}\Plugins\DomePlugIns"; Flags: ignoreversion
Source: "Lunatico.png";                                             DestDir: "{app}\Plugins\DomePlugIns"; Flags: ignoreversion
; 64 bits
Source: "libLunaticoBeaver\x64\Release\libLunaticoBeaver.dll";      DestDir: "{app}\Plugins64\DomePlugIns"; Flags: ignoreversion
Source: "LunaticoBeaver.ui";                                        DestDir: "{app}\Plugins64\DomePlugIns"; Flags: ignoreversion
Source: "NexDome.png";                                              DestDir: "{app}\Plugins64\DomePlugIns"; Flags: ignoreversion
Source: "Lunatico.png";                                             DestDir: "{app}\Plugins64\DomePlugIns"; Flags: ignoreversion

[Code]
{* Below is a function to read TheSkyXInstallPath.txt and confirm that the directory does exist
   This is then used in the DefaultDirName above
   *}
var
  Location: String;
  LoadResult: Boolean;

function TSXInstallDir(Param: String) : String;
begin
  LoadResult := LoadStringFromFile(ExpandConstant('{userdocs}') + '\Software Bisque\TheSkyX Professional Edition\TheSkyXInstallPath.txt', Location);
  if not LoadResult then
    LoadResult := LoadStringFromFile(ExpandConstant('{userdocs}') + '\Software Bisque\TheSky Professional Edition 64\TheSkyXInstallPath.txt', Location);
    if not LoadResult then
      LoadResult := BrowseForFolder('Please locate the installation path for TheSkyX', Location, False);
      if not LoadResult then
        RaiseException('Unable to find the installation path for TheSkyX');
  if not DirExists(Location) then
    RaiseException('TheSkyX installation directory ' + Location + ' does not exist');
  Result := Location;
end;

